OUTPUT_FORMAT("elf32-i386", "elf32-i386", "elf32-i386")
OUTPUT_ARCH(i386)
ENTRY(entry)

/* Kernel virtual address space */
vaddr = 0xc0000000;

/* Load at 1MB */
laddr = 0x00100000;

entry = _start - vaddr;

SECTIONS {
	. = vaddr + laddr;
	__begin = .;

	/* Code section */
	__text = .;
	.text : AT (ADDR(.text) - vaddr) {
		*(.text)
	}
	__text_end = .;

	__rodata = .;
	/* Read only data (strings etc..) */
	.rodata : AT (ADDR(.rodata) - vaddr){
		*(.rodata)
		*(.rodata.*) 
	}
	__rodata_end = .;

	/* Read/write data (global variables) */
	.data = .;
	.data : AT (ADDR(.data) - vaddr) {
		*(.data)
		CONSTRUCTORS
	}
	__data_end = .;

	__bss_start = .;
	.bss : AT (ADDR(.bss) - vaddr) {
		*(.bss)
	}
	__bss_end = .;

	__kernel_end = .;

	/* Init pages */
	. = ALIGN(4096);
	__init_start = .;

	__initcall_start = .;
	.initcall.init : AT (ADDR(.initcall.init) - vaddr) {
		*(.initcall.init)
	}
	__initcall_end = .;

	.text.init : AT (ADDR(.text.init) - vaddr) { *(.text.init) }
	.data.init : AT (ADDR(.data.init) - vaddr) { *(.data.init) }	
	
	/* Thats the lot */
	. = ALIGN(4096);
	__init_end = .;

	__end = . ;

	/* Keep the size down.. */
	/DISCARD/ : {
		*(.comment)
		*(.note)
	}
}
